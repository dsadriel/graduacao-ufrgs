;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-reader.ss" "lang")((modname lista3-AdrielDeSouza-D) (read-case-sensitive #t) (teachpacks ((lib "image.rkt" "teachpack" "2htdp"))) (htdp-settings #(#t constructor repeating-decimal #f #t none #f ((lib "image.rkt" "teachpack" "2htdp")) #f)))
;;Nome: Adriel de Souza - 005*****
;;Fundamento de Algoritmos (INF05008) - Profa. Leila
;;Turma D - 2023/1
;;LISTA 3


;; ============================================
;; DEFINIﾃﾃ髭S DE TIPOS DE DADOS
;; ============================================

;; ------------------
;; TIPO ARQUIVO:
;; ------------------
(define-struct arquivo (nome direitos)) 
;; Um elemento do conjunto Arquivo tem o formato
;;  (make-arquivo n d), onde
;;    n: String, ﾃｩ o nome do arquivo
;;    t: String,  representa os direitos de acesso do usuﾃ｡rio sobre o arquivo,  pode ser "r" (read),"rw" (read/write) e/ou "rwx" (read/write/execute).

;; ------------------
;; TIPO CONTEUDO:
;; ------------------
;; Um Conteudo ﾃｩ
;; 1. empty,
;; 2. (cons a lc), onde a: Arquivo e lc: Conteudo
;; 3. (cons d lc), onde d: Diretorio e lc: Conteudo

;; ------------------
;; TIPO DIRETORIO:
;; ------------------
(define-struct diretorio (nome direitos conteudo))
;; Um elemento do conjunto Diretorio tem o formato
;;  (make-diretorio n d c), onde
;;    n: String, ﾃｩ o nome do diretﾃｳrio
;;    d: String, representa os direitos de acesso do usuﾃ｡rio sobre o diretﾃｳrio,  pode ser "r" (read),"rw" (read/write) e/ou "rwx" (read/write/execute).
;;    c: Conteudo, ﾃｩ o conteﾃｺdo do diretﾃｳrio

;; ============================================
;;                  CONSTANTES
;; ============================================

(define LISTAS
  (make-diretorio "Listas" "rwx"
                  (list
                   (make-arquivo "gera-testes.sh" "rwx")
                   (make-arquivo "lista1.rkt" "rw")
                   (make-arquivo "lista2.rkt" "rw")
                   (make-arquivo "lista3.rkt" "rw")
                   (make-arquivo "lista4.rkt" "rw"))))

(define VIDEOAULAS
  (make-diretorio "Videoaulas" "r"
                  (list
                   (make-arquivo "aula1.mp4" "r")
                   (make-arquivo "aula2.mp4" "r")
                   (make-arquivo "aula3.mp4" "r"))))

(define AULAS
  (make-diretorio "Aulas" "r"
                  (list
                   VIDEOAULAS
                   (make-arquivo "texto1.pdf" "r"))))


(define INF05008 (make-diretorio "INF05008" "rwx"
                                 (list
                                  (make-arquivo "plano-de-ensino.tex" "rw")
                                  (make-arquivo "cronograma.xls" "rw")
                                  AULAS
                                  LISTAS
                                  (make-arquivo "texto1.pdf" "rw"))))

(define DESKTOP (make-diretorio "Desktop" "rwx"
                                (list
                                 (make-arquivo "lista3-2023-1-template.rkt" "rwx")
                                 (make-arquivo "Google Chrome.lnk" "r")
                                 (make-arquivo "Anotaﾃｧﾃｵes.docx" "rwx"))))


;; =========================================================================
;;                                 QUESTﾃグ 1
;; =========================================================================

;; ------------------
;; FUNﾃﾃグ: arquivo-encontrado?
;; ------------------
;; arquivo-encontrado? : Conteudo String -> Booleano
;; Dados o conteﾃｺdo de um diretﾃｳrio e um nome de arquivo, verifica se existe um arquivo com este nome
;; neste conteﾃｺdo considerando subdiretﾃｳrios.

;; Exemplos/testes:
(check-expect (arquivo-encontrado? (diretorio-conteudo INF05008) "texto1.pdf") #t)
(check-expect (arquivo-encontrado? (diretorio-conteudo LISTAS) "texto1.pdf") #f)
(check-expect (arquivo-encontrado? (diretorio-conteudo INF05008) "notas-finais.pdf") #f)
(check-expect (arquivo-encontrado? (list (make-arquivo "notas-finais.pdf" "r") INF05008) "notas-finais.pdf") #t)


(define (arquivo-encontrado? @lista @nome)
  (cond
    ;;Se a lista estiver vazia, devolve falso
    [(empty? @lista) #f]
    [else ;; Caso contrﾃ｡rio
     
     (or ;; OU (i) o arquivo exsite no primeiro elemento da lista
      
      (cond
        ;;Se o primeiro elemento da lita for um diretﾃｳrio
        [(diretorio? (first @lista))
         ;;Entﾃ｣o devolve se o arquivo pode ser encontrado dentro do diretﾃｳrio
         (arquivo-encontrado? (diretorio-conteudo (first @lista)) @nome)]
        
        ;;Caso contrﾃ｡rio, o primeiro elemento ﾃｩ um, devolve verdadeiro se o arquivo tem nome igual ao buscado
        [else
         (string=? (arquivo-nome (first @lista)) @nome)])

      ;; OU (ii) o arquivo existe no resto da lista
        (arquivo-encontrado? (rest @lista) @nome))]))


;; =========================================================================
;;                                 QUESTﾃグ 2
;; =========================================================================


;; ------------------
;; FUNﾃﾃグ: mostra-diretorio
;; ------------------
;; mostra-diretorio : Diretorio -> Imagem
;; Dado um diretﾃｳrio, desenha uma imagem da estrutura deste diretﾃｳrio, apresentando sua estrutura
;; hierﾃ｡rquica, incluindo sub-diretﾃｳrios e arquivos

;; Exemplos:
;; (mostra-diretorio INF05008)
;; (mostra-diretorio DESKTOP)

(define (mostra-diretorio @dir)
  (overlay/align/offset "left" "top" ;;Sobrepﾃｵe alinhado pelo canto superior esquerdo
                        (text ;; Escreve o nome do diretorio a os direitos dele
                         (string-append "刀 "(diretorio-nome @dir) " ("  (diretorio-direitos @dir) ")")
                         15 "Saddle Brown")
                        30 15 ;;Deslocado em 30 e 15 pixels

                        ;;A imagem do conteudo do diretiorio
                        (mostra-conteudo (diretorio-conteudo @dir))))



;; ------------------
;; FUNﾃﾃグ: mostra-conteudo
;; ------------------
;; mostra-conteudo : Conteudo -> Imagem
;; Dado um Conteudo de um diretorio, desenha uma imagem da estrutura deste Conteudo apresentando a
;; estrutura hierﾃ｡rquica, incluindo possﾃｭveis sub-diretﾃｳrios

;; Exemplos:
;; (mostra-conteudo (diretorio-conteudo INF05008)) 
;; (mostra-conteudo (diretorio-conteudo DESKTOP))

(define (mostra-conteudo @lista)
  (cond
    ;;Se a lista estiver vazia, devolve uma imagem vazia
    [(empty? @lista) empty-image]
    [else ;; Caso contrﾃ｡rio
     (above/align ;; Desenha uma acima do outro, alinhado pela esquerda
      "left"

      (cond
        ;;Se o primeiro elemento da lita for um diretﾃｳrio
        [(diretorio? (first @lista))
         ;;A imagem do diretorio
         (mostra-diretorio (first @lista))]
        
        ;;Caso contrﾃ｡rio, devolve uma imagem do modelo arquivo do primeiro elemento do conteudo
        [else
          (text ;;O texto do nome do arquivo e dos direitos
           (string-append "塘 "  (arquivo-nome (first @lista)) " ("  (arquivo-direitos (first @lista)) ")")
           15 "Navy")])

      ;; A imagem do resto do conteﾃｺdo 
      (mostra-conteudo (rest @lista)))]))


;; =========================================================================
;;                                 QUESTﾃグ 3
;; =========================================================================

;; ------------------
;; FUNﾃﾃグ: profundidade
;; ------------------
;; profundidade: Diretorio -> Nﾃｺmero
;; Dado um diretﾃｳrio, devolve a profundidade dele.

;; Exemplos/testes:
(check-expect (profundidade INF05008) 3)
(check-expect (profundidade VIDEOAULAS) 1)
(check-expect (profundidade DESKTOP) 1)

(define (profundidade @dir)
  (cond
    ;;So o conteﾃｺdo do diretorio possuir outro(s) diretﾃｳrio(s), devolve a profundidade do conteﾃｺdo
    [(contem-diretorio? (diretorio-conteudo @dir))
      (profundidade-conteudo (diretorio-conteudo @dir))]
    
    ;;Caso contrﾃ｡rio, devolve 1
    [else 1]))


;; ------------------
;; FUNﾃﾃグ: contem-diretorio?
;; ------------------
;; contem-diretorio? Conteudo -> Booelano
;; Dado um Conteﾃｺdo, devolve verdadeiro caso possua diretﾃｳrio(s) e falso caso contrﾃ｡rio

;; Exemplos/testes:
(check-expect (contem-diretorio?  (diretorio-conteudo INF05008)) #t)
(check-expect (contem-diretorio?  (diretorio-conteudo DESKTOP)) #f)


(define (contem-diretorio? @lista)
  (cond
    ;;Se o conteudo estiver vazio, devolve falso
    [(empty? @lista) #f]
    
    ;;Se o primeiro item da conteudo for um diretﾃｳrio, devolve verdadeiro
    [(diretorio? (first @lista)) #t]
    
    ;;Caso contrﾃ｡rio, verifica so resto da lista comtem diretﾃｳrios
    [else
     (contem-diretorio? (rest @lista))]))


;; ------------------
;; FUNﾃﾃグ: profundidade-conteudo
;; ------------------
;; profundidade-conteudo: Conteudo -> Nﾃｺmero
;; Dado um Conteﾃｺdo de um diretﾃｳrio, devolve a profundidade dele.

;; Exemplos/testes:
(check-expect (profundidade-conteudo (diretorio-conteudo LISTAS)) 1)
(check-expect (profundidade-conteudo (diretorio-conteudo AULAS)) 2)


(define (profundidade-conteudo @lista)
  (cond
    ;;Se a lista estiver vazio, devolve 1
    [(empty? @lista) 1]

    ;;Se o primeiro elemento da lista for um diretﾃｳrio
    [(diretorio? (first @lista))
     (+ 1 ;; Soma 1
     (max ;; Ao mﾃ｡ximo entre
      (profundidade (first @lista)) ;; A profundidade do diretﾃｳrio, representado pelo primeiro elemento da lista e
      (profundidade-conteudo (rest @lista))))] ;; A profundidade do resto da lista

    [else ;; OU devolve a profundidade do resto da lista
      (profundidade-conteudo (rest @lista))]))


;; =========================================================================
;;                                 QUESTﾃグ 4
;; =========================================================================

;; ------------------
;; TIPO DiretorioOUString:
;; ------------------
;; Um DiretorioOUString pde ser
;; 1. Diretorio, ou
;; 2. String.

;; ------------------
;; FUNﾃﾃグ: insere-arquivo
;; ------------------
;; insere-arquivo: Diretorio Arquivo ->  DiretorioOUString
;; Dado um diretﾃｳrio e um arquivo, se o usuﾃ｡rio tiver permissﾃ｣o, insere este arquivo neste diretﾃｳrio.
;; Caso exista arquivo com nome conflitante, substitui esse arquivo.

;; Exemplos/testes:
(check-expect (insere-arquivo VIDEOAULAS (make-arquivo "aula-extra.mp4" "r")) "Usuﾃ｡rio sem permissﾃ｣o para escrever no diretﾃｳrio")
(check-expect (insere-arquivo DESKTOP (make-arquivo "funﾃｧﾃｵes-racket.pdf" "rw"))(make-diretorio "Desktop" "rwx" (list  (make-arquivo "funﾃｧﾃｵes-racket.pdf" "rw") (make-arquivo "lista3-2023-1-template.rkt" "rwx") (make-arquivo "Google Chrome.lnk" "r") (make-arquivo "Anotaﾃｧﾃｵes.docx" "rwx"))))


(define (insere-arquivo @dir @arq)
  (cond
    ;;Se o usuﾃ｡rio tiver somente permissﾃ｣o de leitura, retorna uma string informando o erro
    [(string=? (diretorio-direitos @dir) "r")
      "Usuﾃ｡rio sem permissﾃ｣o para escrever no diretﾃｳrio"]

    ;;Se jﾃ｡ existir um arquivo com o mesmo nome que o arquivo a ser inserido
    [(arquivo-encontrado? (diretorio-conteudo @dir) (arquivo-nome @arq))
     
     (make-diretorio ;;Constroi um diretﾃｳrio com
      (diretorio-nome @dir) ;; Nome inalterado
      (diretorio-direitos @dir) ;;Direitos inalterados
      
      (cons ;; Constroi um novo Conteﾃｺdo com:
       @arq ;; O arquivo a ser inserido e
       ;;O conteﾃｺdo do diretﾃｳrio com o arquivo de mesmo nome removido
       (remove-arquivo (diretorio-conteudo @dir) (arquivo-nome @arq))))]


    ;;Caso contrﾃ｡rio, devolve o diretorio com o arquivo inserido
    [else
     (make-diretorio ;;Constroi um diretﾃｳrio com
      (diretorio-nome @dir) ;; Nome inalterado
      (diretorio-direitos @dir) ;;Direitos inalterados

      (cons ;; Constroi um novo Conteudo com:
       @arq ;; O arquivo a ser inserido
       (diretorio-conteudo @dir)))]))  ;;e o Conteudo jﾃ｡ existente;


;; ------------------
;; FUNﾃﾃグ: remove-arquivo
;; ------------------
;; insere-arquivo: Conteudo String ->  Conteudo
;; Dado um Conteﾃｺdo e um nome da arquivo, remove o arquivo com o nome infornado (se existir) e devolve
;; o conteﾃｺdo.

;; Exemplos/testes:
(check-expect (remove-arquivo (diretorio-conteudo LISTAS) "lista4.rkt") (list (make-arquivo "gera-testes.sh" "rwx") (make-arquivo "lista1.rkt" "rw") (make-arquivo "lista2.rkt" "rw") (make-arquivo "lista3.rkt" "rw")))
(check-expect (remove-arquivo (diretorio-conteudo DESKTOP)"Anotaﾃｧﾃｵes.docx") (list (make-arquivo "lista3-2023-1-template.rkt" "rwx") (make-arquivo "Google Chrome.lnk" "r")))

(define (remove-arquivo @lista @nome)
  (cond
    ;;Se a lista estiver vazia, devolve uma lista vazia
    [(empty? @lista) empty]

    ;;Se o primeiro item da lista for um arquivo E seu nome for igual ao buscado
    [(and
      (arquivo?  (first @lista))
      (string=? (arquivo-nome (first @lista)) @nome))
     ;;Devolve o resto da lista
     (rest @lista)]

    ;;Caso contrﾃ｡rio, constroi um novo Conteudo
    [else
     (cons
      (first @lista) ;;Com o primeiro elemento da lista
      (remove-arquivo (rest @lista) @nome))])) ;;E o resto da lista com o arquivo removido
    



;; =========================================================================
;;                                 QUESTﾃグ 5
;; =========================================================================


;; ------------------
;; FUNﾃﾃグ: mostra-caminho
;; ------------------
;; mostra-caminho: Arquivo Diretorio ->  String
;; Dado um nome de arquivo e um diretﾃｳrio, mostra o caminho para encontrar
;; esse arquivo, ou uma mensagem dizendo que o arquivo nﾃ｣o existe nesse diretﾃｳrio.

;; Exemplos/testes:
(check-expect (mostra-caminho "aula2.mp4" INF05008) "INF05008 -> Aulas -> Videoaulas -> aula2.mp4")
(check-expect(mostra-caminho "lista4.rkt" LISTAS) "Listas -> lista4.rkt")
(check-expect (mostra-caminho "aulax.mp4" INF05008) "Arquivo nﾃ｣o encontrado")


(define (mostra-caminho @nome @dir)
  (cond
    ;;Se o arquivo nﾃ｣o existir no diretﾃｳrio, devolve uma String informando que o arquivo nﾃ｣o foi encontrado
    [(not (arquivo-encontrado? (diretorio-conteudo @dir) @nome))
     "Arquivo nﾃ｣o encontrado"]

    ;;Caso contrﾃ｡rio, entﾃ｣o o arquivo existe
    [else
     (string-append ;; Junta as Strings
      (diretorio-nome @dir) ;; Nome do diretﾃｳrio atual,
      " -> " ;; Indicador de direﾃｧﾃ｣o e
      ;;o caminho para o arquivo com base no conteudo do diretﾃｳrio
      (mostra-caminho-conteudo (diretorio-conteudo @dir) @nome))])) 


;; ------------------
;; FUNﾃﾃグ: mostra-caminho-conteudo
;; ------------------
;; mostra-caminho: Arquivo Conteudo ->  String
;; Dado um nome de arquivo e um conteudo, mostra o caminho para encontrar
;; esse arquivo. Se o arquivo nﾃ｣o existir, devolve uma String vazia

;; Exemplos/testes:
(check-expect (mostra-caminho-conteudo (diretorio-conteudo INF05008) "aula2.mp4" ) "Aulas -> Videoaulas -> aula2.mp4")
(check-expect (mostra-caminho-conteudo (diretorio-conteudo LISTAS) "lista4.rkt" ) "lista4.rkt")
(check-expect (mostra-caminho-conteudo  (diretorio-conteudo INF05008) "aulax.mp4") "")


(define (mostra-caminho-conteudo @lista @nome)
  (cond
    
    ;;Se a lista estiver vazia, devolve uma string vazia
    [(empty? @lista) ""]

    ;;Se primeiro item da lista for um arquivo E seu nome for igual ao buscado
    [(and
      (arquivo?  (first @lista))
      (string=? (arquivo-nome (first @lista)) @nome))
     ;;Devolve o nome do arquivo
     @nome]

    ;;Se o primeiro item da lista for um diretorio E o arquivo pode ser encontrado dentro desse diretﾃｳrio
    [(and
      (diretorio? (first @lista))
      (arquivo-encontrado? (diretorio-conteudo (first @lista)) @nome))
     ;;Devolve o caminho do arquivo dentro do primeiro item da lista
     (mostra-caminho @nome (first @lista))]

    ;;Caso contrﾃ｡rio, mostra o caminho para o arquivo no resto da lista
    [else
     (mostra-caminho-conteudo (rest @lista) @nome)]))